# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Brad T. Aagaard, U.S. Geological Survey
# Charles A. Williams, GNS Science
# Matthew G. Knepley, University of Chicago
#
# This code was developed as part of the Computational Infrastructure
# for Geodynamics (http://geodynamics.org).
#
# Copyright (c) 2010-2017 University of California, Davis
#
# See COPYING for license information.
#
# ----------------------------------------------------------------------
#
# CUBIT journal file with geometry for example showing how to import
# several surfaces (topography, subduction interface, splay fault), and
# mesh the resulting volumes.
#
# We also use the CUBIT support for APREPRO (the expressions in
# braces), which includes units and mathematical functions. See the
# APREPRO section in the appendix of the CUBIT documentation for more
# information.
#
# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
${Units('si')}
#
# ----------------------------------------------------------------------
# Reset geometry.
# ----------------------------------------------------------------------
reset

# ----------------------------------------------------------------------
# Import slab surfaces and construct slab.
# ----------------------------------------------------------------------
import Acis "surf_slabtop.sat"
${idSurf=Id("surface")}
surface {idSurf} name "s_slabtop"

import Acis "surf_slabbot.sat"
${idSurf=Id("surface")}
surface {idSurf} name "s_slabbot"

create volume loft surface s_slabtop s_slabbot
${idVol=Id("volume")}
volume {idVol} name "v_slab"
delete surface s_slabtop s_slabbot

# ----------------------------------------------------------------------
# Create block
# ----------------------------------------------------------------------
# Block is 800 km x 800 km x 400 km
${blockLength=800.0*km}
${blockWidth=800.0*km}
${blockHeight=400.0*km}

brick x {blockLength} y {blockWidth} z {blockHeight}
${idVol=Id("volume")}
volume {idVol} name "v_domain"

# Translate block so top is a z=0.
${moveX=-60.0*km}
${moveY=0.0*km}
volume v_domain move x {moveX} y {moveY} z {-0.5*blockHeight}

# ----------------------------------------------------------------------
# Webcut block with slab.
# ----------------------------------------------------------------------
chop volume v_domain with volume v_slab

# ----------------------------------------------------------------------
# Create planar surface for continental crust/mantle boundary.
# ----------------------------------------------------------------------
create planar surface with plane zplane offset {-40.0*km}
${idSurf=Id("surface")}
surface {idSurf} name "s_moho"

webcut volume v_domain@A with plane surface s_moho
delete surface s_moho


# Create fault patch
${patchLength=200.0*km}
create planar surface with plane yplane offset {-0.5*patchLength}
${idSurf=Id("surface")}
surface {idSurf} name "s_southlimit"

project curve 83 onto surface s_slabtop@B trim
split surface s_slabtop@B across location vertex 59 location vertex 60

create planar surface with plane yplane offset {+0.5*patchLength}
${idSurf=Id("surface")}
surface {idSurf} name "s_northlimit"

project curve 93 onto surface s_slabtop@D trim
split surface s_slabtop@D across location vertex 67 location vertex 68

delete curve all

# ----------------------------------------------------------------------
# Import splay surface and cut volume.
# ----------------------------------------------------------------------
import Acis "surf_splay.sat"
${idSurf=Id("surface")}
surface {idSurf} name "s_splay"

webcut volume v_domain@A with sheet surface s_splay
delete surface s_splay




# ----------------------------------------------------------------------
# Imprint all volumes, then merge.
# ----------------------------------------------------------------------
imprint all with volume all
merge all

# End of file



